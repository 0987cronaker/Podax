buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.1'
        classpath 'com.android.databinding:dataBinder:1.0-rc1'
        classpath 'me.tatarka:gradle-retrolambda:3.2.2'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.7'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'com.android.databinding'
apply plugin: 'me.tatarka.retrolambda'

dependencies {
    compile 'com.android.support:support-v4:23.0.1'
    compile 'com.android.support:support-v13:23.0.1'
    compile 'com.android.support:appcompat-v7:23.0.1'
    compile 'com.android.support:cardview-v7:23.0.1'
    compile 'com.android.support:recyclerview-v7:23.0.1'
    compile 'com.android.support:palette-v7:23.0.1'
    compile 'com.android.support:design:23.0.1'

    compile 'com.facebook.stetho:stetho:1.1.1'

    apt 'com.android.databinding:compiler:1.0-rc1'

    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'com.trello:rxlifecycle:0.1.0'
    compile 'com.trello:rxlifecycle-components:0.1.0'

    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.4.0'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.retrofit:retrofit:1.9.0'

    compile('ch.acra:acra:4.5.0') {
        exclude group: 'org.json'
    }
	compile('org.shredzone.flattr4j:flattr4j-core:2.11') {
        exclude group: 'org.json'
        exclude group: 'org.apache.httpcomponents'
    }
	compile 'com.google.code.findbugs:jsr305:3.0.0'

    // joda-time for android
    // https://github.com/dlew/joda-time-android
    compile 'net.danlew:android.joda:2.8.2'

    compile 'com.joanzapata.android:android-iconify:1.0.6'

    testCompile 'junit:junit:4.12'
}

Properties props = new Properties()
if (new File("signing.properties").exists()) {
    props.load(new FileInputStream(file("signing.properties")))
}

android {
    testOptions {
        unitTests.returnDefaultValues = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

	repositories {
		jcenter()
	}
    compileSdkVersion 23
    buildToolsVersion "23"

    signingConfigs {
        release {
            if (new File("signing.properties").exists()) {
                storeFile file(props['signing.release.keyStore'])
                storePassword props['signing.release.storePassword']
                keyAlias props['signing.release.keyAlias']
                keyPassword props['signing.release.keyPassword']
            }
        }
    }

    defaultConfig {
        applicationId 'com.axelby.podax'
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 65
        versionName '8.0'
    }

    sourceSets.main {
        jni.srcDirs = []
        jniLibs.srcDir 'src/main/libs'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), './proguard.cfg'
            signingConfig signingConfigs.release
        }
    }
}
