buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.+'
    }
}
apply plugin: 'com.android.application'

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
    compile 'com.android.support:support-v4:19.1.+'
    compile 'com.android.support:appcompat-v7:19.1.+'
}

Properties props = new Properties()
if (new File("signing.properties").exists()) {
    props.load(new FileInputStream(file("signing.properties")))
}

android {
    compileSdkVersion 19
    buildToolsVersion "20.0.0"

    defaultConfig {
        applicationId 'com.axelby.podax'
        minSdkVersion 9
        targetSdkVersion 19
        versionCode 62
        versionName '7.05'
    }

    sourceSets.main {
        jni.srcDirs = []
        jniLibs.srcDir 'src/main/libs'
    }

    signingConfigs {
        release {
            if (new File("signing.properties").exists()) {
                storeFile file(props['signing.release.keyStore'])
                storePassword props['signing.release.storePassword']
                keyAlias props['signing.release.keyAlias']
                keyPassword props['signing.release.keyPassword']
            }
        }
    }

    buildTypes {
        release {
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), './proguard.cfg'
            signingConfig signingConfigs.release
        }
    }
}
